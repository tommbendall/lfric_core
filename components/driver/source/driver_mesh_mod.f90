!-----------------------------------------------------------------------------
! (c) Crown copyright 2017 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used
!-----------------------------------------------------------------------------
!>
!> @brief   Set up specified mesh(es) from global/local mesh input file(s).
!> @details This routine will create a mesh_object_type(s) from a
!>          specified mesh input file and extrusion.
!>
!>          The algorithm differs depending on whether the input files(s)
!>          are prepartitioned (local mesh files) or not (global mesh files).
!>
!>          The result will be:
!>            * A set of local mesh objects stored in the application local
!>              mesh collection object.
!>            * A set of mesh objects stored in the application mesh collection
!>              object.
!>
!>          Local mesh object names will use the same mesh name as given in the
!>          input file. Extruded meshes are allowed to have an alternative mesh
!>          name as several meshes could exist in memory based on the same local
!>          mesh object.
!>
module driver_mesh_mod

  use add_mesh_map_mod,           only: assign_mesh_maps
  use constants_mod,              only: i_def, l_def, str_def, &
                                        str_max_filename
  use check_global_mesh_mod,      only: check_global_mesh
  use check_local_mesh_mod,       only: check_local_mesh
  use config_mod,                 only: config_type
  use create_mesh_mod,            only: create_extrusion, create_mesh
  use extrusion_mod,              only: extrusion_type
  use global_mesh_mod,            only: global_mesh_type
  use load_global_mesh_mod,       only: load_global_mesh
  use load_local_mesh_mod,        only: load_local_mesh
  use load_local_mesh_maps_mod,   only: load_local_mesh_maps
  use log_mod,                    only: log_event,         &
                                        log_scratch_space, &
                                        log_level_debug,   &
                                        log_level_error
  use panel_decomposition_mod,    only: panel_decomposition_type
  use partition_mod,              only: partitioner_interface

  use runtime_partition_lfric_mod, only: get_partition_parameters
  use runtime_partition_mod,       only: mesh_cubedsphere,       &
                                         mesh_planar,            &
                                         create_local_mesh_maps, &
                                         create_local_mesh

  use global_mesh_collection_mod, only: global_mesh_collection
  use local_mesh_collection_mod,  only: local_mesh_collection

  ! Configuration modules
  use finite_element_config_mod, only: cellshape_quadrilateral
  use base_mesh_config_mod,      only: geometry_spherical, &
                                       topology_fully_periodic

  implicit none

  private
  public :: init_mesh

contains
!=======================================


!===============================================================================
!> @brief  Generates mesh(es) from mesh input file(s) on a given extrusion.
!>
!> @param[in] config            Application configuration object.
!>                              This configuration object should contain the
!>                              following defined namelist objects:
!>                                 * base_mesh
!>                                 * finite_element
!>                                 * partititioning
!> @param[in] local_rank        The MPI rank of this process.
!> @param[in] total_ranks       Total number of MPI ranks in this job.
!> @param[in] mesh_names        Mesh names to load from the mesh input file(s).
!> @param[in] extrusion         Extrusion object to be applied to meshes.
!> @param[in] inner_halo_tiles  Apply tiling to inner halos.
!> @param[in] tile_size         Tile sizes to apply to inner halos if applicable.
!> @param[in] stencil_depths_in Required stencil depth for each mesh for
!!                              the application. If this array is of size 1 then
!!                              the single value is applied to all meshes.
!!                              Otherwise the array size must match the size
!!                              the mesh name array, allowing different depths
!!                              to be specified for different meshes.
!> @param[in] check_partitions  Apply check for even partitions with the
!>                              configured partition stratedy.
!>                              (unpartitioned mesh input only)
!> @param[in] alt_names         (Optional), Alternative names for meshes in the
!>                                          application mesh collection object.
!===============================================================================
subroutine init_mesh( config,                  &
                      local_rank, total_ranks, &
                      mesh_names, extrusion,   &
                      inner_halo_tiles,        &
                      tile_size,               &
                      stencil_depths_in,       &
                      check_partitions,        &
                      alt_names )

  implicit none

  ! Arguments
  type(config_type),     intent(in) :: config
  integer(i_def),        intent(in) :: local_rank
  integer(i_def),        intent(in) :: total_ranks
  character(str_def),    intent(in) :: mesh_names(:)
  class(extrusion_type), intent(in) :: extrusion

  logical(l_def), intent(in) :: inner_halo_tiles
  integer(i_def), intent(in) :: tile_size(:,:)
  integer(i_def), intent(in) :: stencil_depths_in(:)
  logical(l_def), intent(in) :: check_partitions

  character(str_def), optional, intent(in) :: alt_names(:)

  ! Parameters
  character(len=9), parameter :: routine_name = 'init_mesh'

  ! Namelist variables
  character(str_max_filename)  :: file_prefix

  integer(i_def) :: cellshape

  logical(l_def) :: prepartitioned
  logical(l_def) :: generate_inner_halos

  integer :: geometry
  integer :: topology
  integer :: mesh_selection

  ! Local variables
  character(str_def), allocatable :: names(:)
  character(str_def), allocatable :: tmp_mesh_names(:)
  character(str_max_filename)     :: input_mesh_file
  integer(i_def),     allocatable :: stencil_depths(:)

  procedure(partitioner_interface), pointer :: partitioner_ptr

  class(panel_decomposition_type), allocatable :: decomposition

  character(str_def) :: fmt_str, number_str

  integer(i_def) :: i, n_digit

  !============================================================================
  ! Extract configuration variables
  !============================================================================
  prepartitioned = config%base_mesh%prepartitioned()
  file_prefix    = config%base_mesh%file_prefix()
  cellshape      = config%finite_element%cellshape()

  if ( .not. prepartitioned ) then
    generate_inner_halos = config%partitioning%generate_inner_halos()
  end if


  !============================================================================
  ! Some basic checks
  !============================================================================

  if ( size(stencil_depths_in) == 1 ) then
    ! Single stencil depth specified, apply to all meshes
    allocate( stencil_depths( size(mesh_names) ) )
    do i = 1, size(mesh_names)
      stencil_depths(i) = stencil_depths_in(1)
    end do
  else if ( size(stencil_depths_in) == size(mesh_names) ) then
    ! Stencil depths specified per mesh
    allocate( stencil_depths( size(mesh_names) ) )
    stencil_depths = stencil_depths_in
  else
    write(log_scratch_space, '(A)')                      &
        'Number of stencil depths specified does not '// &
        'match number of requested meshes.'
    call log_event(log_scratch_space, LOG_LEVEL_ERROR)
  end if

  ! Check stencil depths are valid
  do i = 1, size(stencil_depths)
    if (stencil_depths(i) < 0_i_def) then
      write(log_scratch_space,'(A)') &
        'Standard partitioned meshes must support a not -ve stencil_depth'
      call log_event(log_scratch_space, LOG_LEVEL_ERROR)
    end if
  end do

  ! Currently only quad elements are fully functional
  if (cellshape /= cellshape_quadrilateral) then
    call log_event( "Reference_element must be QUAD for now...", &
                    LOG_LEVEL_ERROR )
  end if


  !============================================================================
  ! Determine which names to apply to resultant meshes.
  !============================================================================
  if (present(alt_names)) then
    if (size(alt_names) == size(mesh_names)) then
      allocate(names, source=alt_names)
    else
      write(log_scratch_space, '(A)')                   &
          'Specified alternative mesh names to does '// &
          'not match number of requested meshes.'
      call log_event(log_scratch_space, LOG_LEVEL_ERROR)
    end if
  else
    allocate(names, source=mesh_names)
  end if


  !===========================================================================
  ! Create local mesh objects:
  !   Two code pathes presented, either:
  !   1. The input files have been pre-partitioned.
  !      Meshes and are simply read from file and local mesh objects
  !      are populated.
  !   2. The input files have not been partitioned.
  !      Global meshes are loaded from file and partitioning is applied
  !      at runtime.  NOTE: This option is provided as legacy, and support
  !      is on a best endeavours basis.
  !===========================================================================

  generate_inner_halos = .false.

  if (prepartitioned) then

    !==========================================================================
    ! Read in local meshes / partition information / mesh maps
    ! direct from file.
    !==========================================================================
    !
    ! For this local rank, a mesh input file with a common base name
    ! of the following form should exist.
    !
    !   <input_basename>_<local_rank>_<total_ranks>.nc
    !
    ! Where 1 rank is assigned to each mesh partition.
    n_digit = int(log10(real(total_ranks))) + 1
    write(fmt_str, '(A,I0,A,I0,A)') "(I", n_digit, ".", n_digit, ")"
    write(number_str, fmt_str) local_rank
    write(input_mesh_file, '(A, "_", A, "-", I0, ".nc")') &
        trim(file_prefix), trim(number_str), total_ranks

    call log_event( 'Using pre-partitioned mesh file:', log_level_debug )
    call log_event( '   '//trim(input_mesh_file), log_level_debug )
    call log_event( "Loading local mesh(es)", log_level_debug )


    ! Read in all local mesh data for this rank and
    ! initialise local mesh objects from them.
    !===========================================================
    ! Each partitioned mesh file will contain meshes of the
    ! same name as all other partitions.
    call load_local_mesh( input_mesh_file, mesh_names )

    ! Apply configuration related checks to ensure that these
    ! meshes are suitable for the supplied application
    ! configuration.
    !===========================================================
    call check_local_mesh( config,         &
                           stencil_depths, &
                           mesh_names )

    ! Load and assign mesh maps.
    !===========================================================
    ! Mesh map identifiers are determined by the source/target
    ! mesh IDs they relate to. As a result inter-grid mesh maps
    ! need to be loaded after the relevant local meshes have
    ! been loaded.
    tmp_mesh_names = local_mesh_collection%get_mesh_names()
    call load_local_mesh_maps( input_mesh_file, tmp_mesh_names )
    if (allocated(tmp_mesh_names)) deallocate(tmp_mesh_names)

  else

    !==========================================================================
    ! Perform runtime partitioning of global meshes.
    !==========================================================================
    geometry = config%base_mesh%geometry()
    topology = config%base_mesh%topology()

    generate_inner_halos = config%partitioning%generate_inner_halos()

    if ( geometry == geometry_spherical .and. &
         topology == topology_fully_periodic ) then
      mesh_selection = mesh_cubedsphere
      call log_event( "Setting up cubed-sphere partition mesh(es)", &
                      log_level_debug )
    else
      mesh_selection = mesh_planar
      call log_event( "Setting up planar partition mesh(es)", &
                      log_level_debug )
    end if
    write(input_mesh_file,'(A)') trim(file_prefix) // '.nc'

    ! 2.2a Set constants that will control partitioning.
    !===========================================================
    call get_partition_parameters( config%partitioning, mesh_selection, &
                                   total_ranks, decomposition,          &
                                   partitioner_ptr )

    ! 2.2b Read in all global meshes from input file
    !===========================================================
    call load_global_mesh( input_mesh_file, mesh_names )

    ! 2.2c Apply configuration related checks to ensure that these
    !      meshes are suitable for the supplied application
    !      configuration.
    !===========================================================
    call check_global_mesh( config, mesh_names )

    ! 2.2e Partition the global meshes
    !===========================================================
    call create_local_mesh( mesh_names,              &
                            local_rank, total_ranks, &
                            decomposition,           &
                            stencil_depths,          &
                            generate_inner_halos,    &
                            partitioner_ptr,         &
                            enforce_constraints=check_partitions )


    ! 2.2f Read in the global intergrid mesh mappings,
    !      then create the associated local mesh maps
    !===========================================================
    call create_local_mesh_maps( input_mesh_file )

    ! Clear the global mesh
    call global_mesh_collection%clear()

  end if  ! prepartitioned

  !============================================================================
  ! 3.0 Extrude the specified meshes from local mesh objects into
  !     mesh objects on the given extrusion.
  !============================================================================
  call create_mesh( mesh_names, extrusion,       &
                    inner_halo_tiles, tile_size, &
                    alt_name=names )


  !============================================================================
  ! 4.0 Generate intergrid LiD-LiD maps and assign them to mesh objects.
  !============================================================================
  call assign_mesh_maps(mesh_names)

  deallocate(stencil_depths)

end subroutine init_mesh

end module driver_mesh_mod
